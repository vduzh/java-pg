plugins {
    alias(libs.plugins.liquibase.plugin) apply false
}

def configureLiquibase(Project project) {
    project.liquibase {
        activities {
            main {
                arguments = [
                        searchPath   : "${project.projectDir}",
                        changelogFile: "changelog-master.yaml",
                        url          : 'jdbc:postgresql://localhost:5433/postgres',
                        driver       : 'org.postgresql.Driver'
                ]
            }
        }
        runList = 'main'
    }
}

subprojects {
    apply plugin: 'org.liquibase.gradle'

    dependencies {
        liquibaseRuntime libs.liquibase.core
        liquibaseRuntime libs.postgresql
        liquibaseRuntime libs.picocli
    }

    configureLiquibase(project)
}

//dependencies {
//    liquibaseRuntime libs.liquibase.core
//    liquibaseRuntime libs.postgresql
//    liquibaseRuntime libs.picocli
//}

task updateAll {
    group = 'Liquibase'
    description = 'Run Liquibase migrations for all subprojects in db-migrations.'
    dependsOn subprojects.collect { "${it.path}:update" }
}
